import os
from building import *

cwd = GetCurrentDir()

Import('asenv')
ASROOT = asenv['ASROOT']

print('native run command:scons run')
if('run' in COMMAND_LINE_TARGETS):
    build = '%s/release/%s'%(ASROOT,asenv['RELEASE'])
    if(os.name == 'nt'):
        if(not asenv['msys2']):
            RunCommand('%s -O2 -shared -Wall -o %s/backtrace.dll %s/com/as.infrastructure/diagnostic/Det/backtrace.c -lbfd -lintl -liberty -limagehlp -D__ASDEBUG_BACKTRACE__'
                   %(asenv['CC'],build,ASROOT))
        extfs = '%s/ExtFs.img'%(build)
        if(asenv['msys2'] and (asenv['RELEASE'] == 'ascore') and (not os.path.exists(extfs))):
            lwext4mkfs = '%s/release/download/lwext4/build_generic/fs_test/lwext4-mkfs.exe'%(ASROOT)
            RunCommand('echo "0" > %s && truncate -s 33554432 %s'%(extfs,extfs))
            RunCommand('%s -i %s -b 4096 -e 4'%(lwext4mkfs,extfs))
        RunCommand('cd %s/com/as.tool/as.one.py && start %s main.py'%(ASROOT,asenv['python3']))
        RunCommand('start %s/com/as.tool/lua/can/socketwin_can_driver.exe 0'%(ASROOT))
        RunCommand('start %s/com/as.tool/lua/can/socketwin_can_driver.exe 1'%(ASROOT))
    target = '%s/release/%s/posix'%(ASROOT,asenv['RELEASE'])
    if(os.name != 'nt'):
        target = 'sudo ' + target
    RunCommand('cd %s && %s'%(build,target))
    exit(0)

MODULES = ['ECUM','SCHM','MCU',
           'DET','DEM','SHELL',
           'CAN','CANIF','PDUR','COM','COMM','CANTP',
           'DCM','CANNM','CANSM','NM','OSEKNM','XCP',
           'PORT','DIO','EEP','FLS','STMO',
           'EA','FEE','MEMIF','NVM',
           'AWS','LCD',
           ]

ARCH='posix'
arch='posix'

MODULES += ['SOAD','DOIP','LWIP']

MODULES += ['FATFS','LWEXT4','VFS','FTP']

if(os.name == 'nt'):
    MODULES += ['CLIB_STRTOK_R']

MODULES += ['CLIB_MBOX']
MODULES += ['CLIB_MISCLIB']
MODULES += ['CLIB_STDIO_PRINTF']
MODULES += ['CLIB_STDIO_CAN']

MODULES += ['RTE','RTE_SWC_TELLTALE']

if(cwd.find('travis') != -1):
    # this is on travis, GUI is not possible as rely on QT to generate pictures to C source code
    MODULES.remove('LCD')
else:
    if(os.getenv('GUI') in ['GTK','OPENVG']):
        MODULES.append(os.getenv('GUI'))
    else:
        print('WARNING: use default GTK GUI for posix')
        MODULES.append('GTK')

    MODULES.append('SG')

# post special process for asboot release
if(asenv['RELEASE']=='asboot'):
    MODULES = ['ECUM','SCHM','MCU','DET',
               'CAN','CANIF','PDUR','CANTP',
               'DCM',
               'FLASH',
               ]

if(asenv['MODULES'] is None):
    asenv['MODULES'] = MODULES
else:
    MODULES = asenv['MODULES']

asenv['ARCH'] = ARCH
asenv['arch'] = arch

if(asenv['RTOS'] != 'small'):
    print('WARNING: force RTOS to "small" instead of "%s"'%(asenv['RTOS']))
    asenv['RTOS']='small'

objs = []

asenv.Append(CPPPATH=['%s/common'%(cwd)])

if('DIO' in MODULES):
    objs += Glob('common/Dio*.c')

if('EEP' in MODULES):
    objs += Glob('common/Eep*.c')

if('IPC' in MODULES):
    objs += Glob('common/Ipc*.c')

if('MCU' in MODULES):
    objs += Glob('common/Mcu*.c')

if('PORT' in MODULES):
    objs += Glob('common/Port*.c')

if('STMO' in MODULES):
    objs += Glob('common/Stmo*.c')

objs += Glob('simulator/*.c')
objs += Glob('simulator/*.xml')



Return('objs')
